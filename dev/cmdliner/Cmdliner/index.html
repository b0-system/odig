<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml"><head><title>Cmdliner (cmdliner.Cmdliner)</title><link rel="stylesheet" href="../../_odoc-theme/odoc.css"/><meta charset="utf-8"/><meta name="viewport" content="width=device-width,initial-scale=1.0"/><script src="../../highlight.pack.js"></script><script>hljs.initHighlightingOnLoad();</script></head><body><div class="content"><header><nav><a href="../index.html">Up</a> – <a href="../index.html">cmdliner</a> &#x00BB; Cmdliner</nav><h1>Module <code>Cmdliner</code></h1><p>Declarative definition of command line interfaces.</p><p><code>Cmdliner</code> provides a simple and compositional mechanism to convert command line arguments to OCaml values and pass them to your functions. The module automatically handles syntax errors, help messages and UNIX man page generation. It supports programs with single or multiple commands (like <code>darcs</code> or <code>git</code>) and respect most of the <a href="http://www.opengroup.org/onlinepubs/009695399/basedefs/xbd_chap12.html">POSIX</a> and <a href="http://www.gnu.org/software/libc/manual/html_node/Argument-Syntax.html">GNU</a> conventions.</p><p>Consult the <a href="index.html#basics"><span>basics</span></a>, details about the supported <a href="index.html#cmdline"><span>command line syntax</span></a> and <a href="index.html#examples"><span>examples</span></a> of use. Open the module to use it, it defines only three modules in your scope.</p><p><em>v1.0.3 — <a href="http://erratique.ch/software/cmdliner">homepage</a></em></p><nav class="toc"><ul><li><a href="#top">Interface</a></li><li><a href="#basics">Basics</a><ul><li><a href="#multiterms">Multiple terms</a></li><li><a href="#doclang">Documentation markup language</a></li><li><a href="#manual">Manual</a></li><li><a href="#misc">Miscellaneous</a></li></ul></li><li><a href="#cmdline">Command line syntax</a><ul><li><a href="#optargs">Optional arguments</a></li><li><a href="#posargs">Positional arguments</a></li><li><a href="#envlookup">Environment variables</a></li></ul></li><li><a href="#examples">Examples</a><ul><li><a href="#exrm">A <code>rm</code> command</a></li><li><a href="#excp">A <code>cp</code> command</a></li><li><a href="#extail">A <code>tail</code> command</a></li><li><a href="#exdarcs">A <code>darcs</code> command</a></li></ul></li></ul></nav></header><section><header><h2 id="top"><a href="#top" class="anchor"></a>Interface</h2></header><dl><dt class="spec module" id="module-Manpage"><a href="#module-Manpage" class="anchor"></a><code><span class="keyword">module</span> <a href="Manpage/index.html">Manpage</a> : <span class="keyword">sig</span> ... <span class="keyword">end</span></code></dt><dd><p>Man page specification.</p></dd></dl><dl><dt class="spec module" id="module-Term"><a href="#module-Term" class="anchor"></a><code><span class="keyword">module</span> <a href="Term/index.html">Term</a> : <span class="keyword">sig</span> ... <span class="keyword">end</span></code></dt><dd><p>Terms.</p></dd></dl><dl><dt class="spec module" id="module-Arg"><a href="#module-Arg" class="anchor"></a><code><span class="keyword">module</span> <a href="Arg/index.html">Arg</a> : <span class="keyword">sig</span> ... <span class="keyword">end</span></code></dt><dd><p>Terms for command line arguments.</p></dd></dl></section><section><header><h2 id="basics"><a href="#basics" class="anchor"></a>Basics</h2><p>With <code>Cmdliner</code> your program evaluates a term. A <em>term</em> is a value of type <a href="Term/index.html#type-t"><code>Term.t</code></a>. The type parameter indicates the type of the result of the evaluation.</p><p>One way to create terms is by lifting regular OCaml values with <a href="Term/index.html#val-const"><code>Term.const</code></a>. Terms can be applied to terms evaluating to functional values with <span class="xref-unresolved" title="unresolved reference to &quot;Term.( $ )&quot;"><a href="index.html#module-Term"><code>Term</code></a>.( $ )</span>. For example for the function:</p><pre><code class="ml">let revolt () = print_endline &quot;Revolt!&quot;</code></pre><p>the term :</p><pre><code class="ml">open Cmdliner

let revolt_t = Term.(const revolt $ const ())</code></pre><p>is a term that evaluates to the result (and effect) of the <code>revolt</code> function. Terms are evaluated with <a href="Term/index.html#val-eval"><code>Term.eval</code></a>:</p><pre><code class="ml">let () = Term.exit @@ Term.eval (revolt_t, Term.info &quot;revolt&quot;)</code></pre><p>This defines a command line program named <code>&quot;revolt&quot;</code>, without command line arguments, that just prints <code>&quot;Revolt!&quot;</code> on <code>stdout</code>.</p><pre><code class="ml">&gt; ./revolt
Revolt!</code></pre><p>The combinators in the <a href="Arg/index.html"><code>Arg</code></a> module allow to extract command line argument data as terms. These terms can then be applied to lifted OCaml functions to be evaluated by the program.</p><p>Terms corresponding to command line argument data that are part of a term evaluation implicitly define a command line syntax. We show this on an concrete example.</p><p>Consider the <code>chorus</code> function that prints repeatedly a given message :</p><pre><code class="ml">let chorus count msg =
  for i = 1 to count do print_endline msg done</code></pre><p>we want to make it available from the command line with the synopsis:</p><pre><code class="ml">chorus [-c COUNT | --count=COUNT] [MSG]</code></pre><p>where <code>COUNT</code> defaults to <code>10</code> and <code>MSG</code> defaults to <code>&quot;Revolt!&quot;</code>. We first define a term corresponding to the <code>--count</code> option:</p><pre><code class="ml">let count =
  let doc = &quot;Repeat the message $(docv) times.&quot; in
  Arg.(value &amp; opt int 10 &amp; info [&quot;c&quot;; &quot;count&quot;] ~docv:&quot;COUNT&quot; ~doc)</code></pre><p>This says that <code>count</code> is a term that evaluates to the value of an optional argument of type <code>int</code> that defaults to <code>10</code> if unspecified and whose option name is either <code>-c</code> or <code>--count</code>. The arguments <code>doc</code> and <code>docv</code> are used to generate the option's man page information.</p><p>The term for the positional argument <code>MSG</code> is:</p><pre><code class="ml">let msg =
  let doc = &quot;Overrides the default message to print.&quot; in
  let env = Arg.env_var &quot;CHORUS_MSG&quot; ~doc in
  let doc = &quot;The message to print.&quot; in
  Arg.(value &amp; pos 0 string &quot;Revolt!&quot; &amp; info [] ~env ~docv:&quot;MSG&quot; ~doc)</code></pre><p>which says that <code>msg</code> is a term whose value is the positional argument at index <code>0</code> of type <code>string</code> and defaults to <code>&quot;Revolt!&quot;</code> or the value of the environment variable <code>CHORUS_MSG</code> if the argument is unspecified on the command line. Here again <code>doc</code> and <code>docv</code> are used for the man page information.</p><p>The term for executing <code>chorus</code> with these command line arguments is :</p><pre><code class="ml">let chorus_t = Term.(const chorus $ count $ msg)</code></pre><p>and we are now ready to define our program:</p><pre><code class="ml">let info =
  let doc = &quot;print a customizable message repeatedly&quot; in
  let man = [
    `S Manpage.s_bugs;
    `P &quot;Email bug reports to &lt;hehey at example.org&gt;.&quot; ]
  in
  Term.info &quot;chorus&quot; ~version:&quot;%‌%VERSION%%&quot; ~doc ~exits:Term.default_exits ~man

let () = Term.exit @@ Term.eval (chorus_t, info))</code></pre><p>The <code>info</code> value created with <a href="Term/index.html#type-info"><code>Term.info</code></a> gives more information about the term we execute and is used to generate the program's man page. Since we provided a <code>~version</code> string, the program will automatically respond to the <code>--version</code> option by printing this string.</p><p>A program using <a href="Term/index.html#val-eval"><code>Term.eval</code></a> always responds to the <code>--help</code> option by showing the man page about the program generated using the information you provided with <a href="Term/index.html#type-info"><code>Term.info</code></a> and <a href="Arg/index.html#type-info"><code>Arg.info</code></a>. Here is the output generated by our example :</p><pre>&gt; ./chorus --help
NAME
       chorus - print a customizable message repeatedly

SYNOPSIS
       chorus [OPTION]... [MSG]

ARGUMENTS
       MSG (absent=Revolt! or CHORUS_MSG env)
           The message to print.

OPTIONS
       -c COUNT, --count=COUNT (absent=10)
           Repeat the message COUNT times.

       --help[=FMT] (default=auto)
           Show this help in format FMT. The value FMT must be one of `auto',
           `pager', `groff' or `plain'. With `auto', the format is `pager` or
           `plain' whenever the TERM env var is `dumb' or undefined.

       --version
           Show version information.

EXIT STATUS
       chorus exits with the following status:

       0   on success.

       124 on command line parsing errors.

       125 on unexpected internal errors (bugs).

ENVIRONMENT
       These environment variables affect the execution of chorus:

       CHORUS_MSG
           Overrides the default message to print.

BUGS
       Email bug reports to &lt;hehey at example.org&gt;.</pre><p>If a pager is available, this output is written to a pager. This help is also available in plain text or in the <a href="http://www.gnu.org/software/groff/groff.html">groff</a> man page format by invoking the program with the option <code>--help=plain</code> or <code>--help=groff</code>.</p><p>For examples of more complex command line definitions look and run the <a href="index.html#examples"><span>examples</span></a>.</p></header><section><header><h3 id="multiterms"><a href="#multiterms" class="anchor"></a>Multiple terms</h3><p><code>Cmdliner</code> also provides support for programs like <code>darcs</code> or <code>git</code> that have multiple commands each with their own syntax:</p><pre><code class="ml">prog COMMAND [OPTION]... ARG...</code></pre><p>A command is defined by coupling a term with <a href="Term/index.html#tinfo"><span>term information</span></a>. The term information defines the command name and its man page. Given a list of commands the function <a href="Term/index.html#val-eval_choice"><code>Term.eval_choice</code></a> will execute the term corresponding to the <code>COMMAND</code> argument or a specific &quot;main&quot; term if there is no <code>COMMAND</code> argument.</p></header></section><section><header><h3 id="doclang"><a href="#doclang" class="anchor"></a>Documentation markup language</h3><p>Manpage <a href="Manpage/index.html#type-block"><span>blocks</span></a> and doc strings support the following markup language.</p><ul><li>Markup directives <code>$(i,text)</code> and <code>$(b,text)</code>, where <code>text</code> is raw text respectively rendered in italics and bold.</li><li>Outside markup directives, context dependent variables of the form <code>$(var)</code> are substituted by marked up data. For example in a term's man page <code>$(tname)</code> is substituted by the term name in bold.</li><li>Characters $, (, ) and \ can respectively be escaped by \$, \(, \) and \\ (in OCaml strings this will be <code>&quot;\\$&quot;</code>, <code>&quot;\\(&quot;</code>, <code>&quot;\\)&quot;</code>, <code>&quot;\\\\&quot;</code>). Escaping $ and \ is mandatory everywhere. Escaping ) is mandatory only in markup directives. Escaping ( is only here for your symmetric pleasure. Any other sequence of characters starting with a \ is an illegal character sequence.</li><li>Refering to unknown markup directives or variables will generate errors on standard error during documentation generation.</li></ul></header></section><section><header><h3 id="manual"><a href="#manual" class="anchor"></a>Manual</h3><p>Man page sections for a term are printed in the order specified by the term manual as given to <a href="Term/index.html#type-info"><code>Term.info</code></a>. Unless specified explicitely in the term's manual the following sections are automaticaly created and populated for you:</p><ul><li><a href="Manpage/index.html#val-s_name"><span><code>NAME</code></span></a> section.</li><li><a href="Manpage/index.html#val-s_synopsis"><span><code>SYNOPSIS</code></span></a> section.</li></ul><p>The various <code>doc</code> documentation strings specified by the term's subterms and additional metadata get inserted at the end of the documentation section name <code>docs</code> they respectively mention, in the following order:</p><ol><li>Commands, see <a href="Term/index.html#type-info"><code>Term.info</code></a>.</li><li>Positional arguments, see <a href="Arg/index.html#type-info"><code>Arg.info</code></a>. Those are listed iff both the <code>docv</code> and <code>doc</code> string is specified by <a href="Arg/index.html#type-info"><code>Arg.info</code></a>.</li><li>Optional arguments, see <a href="Arg/index.html#type-info"><code>Arg.info</code></a>.</li><li>Exit statuses, see <a href="Term/index.html#type-exit_info"><code>Term.exit_info</code></a>.</li><li>Environment variables, see <a href="Arg/index.html#val-env_var"><code>Arg.env_var</code></a> and <a href="Term/index.html#type-env_info"><code>Term.env_info</code></a>.</li></ol><p>If a <code>docs</code> section name is mentioned and does not exist in the term's manual, an empty section is created for it, after which the <code>doc</code> strings are inserted, possibly prefixed by boilerplate text (e.g. for <a href="Manpage/index.html#val-s_environment"><code>Manpage.s_environment</code></a> and <a href="Manpage/index.html#val-s_exit_status"><code>Manpage.s_exit_status</code></a>).</p><p>If the created section is:</p><ul><li><a href="Manpage/index.html#standard_sections"><span>standard</span></a>, it is inserted at the right place in the order specified <a href="Manpage/index.html#standard_sections"><span>here</span></a>, but after a possible non-standard section explicitely specified by the term since the latter get the order number of the last previously specified standard section or the order of <a href="Manpage/index.html#val-s_synopsis"><code>Manpage.s_synopsis</code></a> if there is no such section.</li><li>non-standard, it is inserted before the <a href="Manpage/index.html#val-s_commands"><code>Manpage.s_commands</code></a> section or the first subsequent existing standard section if it doesn't exist. Taking advantage of this behaviour is discouraged, you should declare manually your non standard section in the term's manual.</li></ul><p>Ideally all manual strings should be UTF-8 encoded. However at the moment macOS (until at least 10.12) is stuck with <code>groff 1.19.2</code> which doesn't support `preconv(1)`. Regarding UTF-8 output, generating the man page with <code>-Tutf8</code> maps the hyphen-minus <code>U+002D</code> to the minus sign <code>U+2212</code> which makes it difficult to search it in the pager, so <code>-Tascii</code> is used for now. Conclusion is that it is better to stick to the ASCII set for now. Please contact the author if something seems wrong in this reasoning or if you know a work around this.</p></header></section><section><header><h3 id="misc"><a href="#misc" class="anchor"></a>Miscellaneous</h3><ul><li>The option name <code>--cmdliner</code> is reserved by the library.</li><li>The option name <code>--help</code>, (and <code>--version</code> if you specify a version string) is reserved by the library. Using it as a term or option name may result in undefined behaviour.</li><li>Defining the same option or command name via two different arguments or terms is illegal and raises <code>Invalid_argument</code>.</li></ul></header></section></section><section><header><h2 id="cmdline"><a href="#cmdline" class="anchor"></a>Command line syntax</h2><p>For programs evaluating a single term the most general form of invocation is:</p><pre><code class="ml">prog [OPTION]... [ARG]...</code></pre><p>The program automatically reponds to the <code>--help</code> option by printing the help. If a version string is provided in the <a href="Term/index.html#tinfo"><span>term information</span></a>, it also automatically responds to the <code>--version</code> option by printing this string.</p><p>Command line arguments are either <a href="index.html#optargs"><span><em>optional</em></span></a> or <a href="index.html#posargs"><span><em>positional</em></span></a>. Both can be freely interleaved but since <code>Cmdliner</code> accepts many optional forms this may result in ambiguities. The special <a href="index.html#posargs"><span>token <code>--</code></span></a> can be used to resolve them.</p><p>Programs evaluating multiple terms also add this form of invocation:</p><pre><code class="ml">prog COMMAND [OPTION]... [ARG]...</code></pre><p>Commands automatically respond to the <code>--help</code> option by printing their help. The <code>COMMAND</code> string must be the first string following the program name and may be specified by a prefix as long as it is not ambiguous.</p></header><section><header><h3 id="optargs"><a href="#optargs" class="anchor"></a>Optional arguments</h3><p>An optional argument is specified on the command line by a <em>name</em> possibly followed by a <em>value</em>.</p><p>The name of an option can be short or long.</p><ul><li>A <em>short</em> name is a dash followed by a single alphanumeric character: <code>&quot;-h&quot;</code>, <code>&quot;-q&quot;</code>, <code>&quot;-I&quot;</code>.</li><li>A <em>long</em> name is two dashes followed by alphanumeric characters and dashes: <code>&quot;--help&quot;</code>, <code>&quot;--silent&quot;</code>, <code>&quot;--ignore-case&quot;</code>.</li></ul><p>More than one name may refer to the same optional argument. For example in a given program the names <code>&quot;-q&quot;</code>, <code>&quot;--quiet&quot;</code> and <code>&quot;--silent&quot;</code> may all stand for the same boolean argument indicating the program to be quiet. Long names can be specified by any non ambiguous prefix.</p><p>The value of an option can be specified in three different ways.</p><ul><li>As the next token on the command line: <code>&quot;-o a.out&quot;</code>, <code>&quot;--output a.out&quot;</code>.</li><li>Glued to a short name: <code>&quot;-oa.out&quot;</code>.</li><li>Glued to a long name after an equal character: <code>&quot;--output=a.out&quot;</code>.</li></ul><p>Glued forms are especially useful if the value itself starts with a dash as is the case for negative numbers, <code>&quot;--min=-10&quot;</code>.</p><p>An optional argument without a value is either a <em>flag</em> (see <a href="Arg/index.html#val-flag"><code>Arg.flag</code></a>, <a href="Arg/index.html#val-vflag"><code>Arg.vflag</code></a>) or an optional argument with an optional value (see the <code>~vopt</code> argument of <a href="Arg/index.html#val-opt"><code>Arg.opt</code></a>).</p><p>Short flags can be grouped together to share a single dash and the group can end with a short option. For example assuming <code>&quot;-v&quot;</code> and <code>&quot;-x&quot;</code> are flags and <code>&quot;-f&quot;</code> is a short option:</p><ul><li><code>&quot;-vx&quot;</code> will be parsed as <code>&quot;-v -x&quot;</code>.</li><li><code>&quot;-vxfopt&quot;</code> will be parsed as <code>&quot;-v -x -fopt&quot;</code>.</li><li><code>&quot;-vxf opt&quot;</code> will be parsed as <code>&quot;-v -x -fopt&quot;</code>.</li><li><code>&quot;-fvx&quot;</code> will be parsed as <code>&quot;-f=vx&quot;</code>.</li></ul></header></section><section><header><h3 id="posargs"><a href="#posargs" class="anchor"></a>Positional arguments</h3><p>Positional arguments are tokens on the command line that are not option names and are not the value of an optional argument. They are numbered from left to right starting with zero.</p><p>Since positional arguments may be mistaken as the optional value of an optional argument or they may need to look like option names, anything that follows the special token <code>&quot;--&quot;</code> on the command line is considered to be a positional argument.</p></header></section><section><header><h3 id="envlookup"><a href="#envlookup" class="anchor"></a>Environment variables</h3><p>Non-required command line arguments can be backed up by an environment variable. If the argument is absent from the command line and that the environment variable is defined, its value is parsed using the argument converter and defines the value of the argument.</p><p>For <a href="Arg/index.html#val-flag"><code>Arg.flag</code></a> and <a href="Arg/index.html#val-flag_all"><code>Arg.flag_all</code></a> that do not have an argument converter a boolean is parsed from the lowercased variable value as follows:</p><ul><li><code>&quot;&quot;</code>, <code>&quot;false&quot;</code>, <code>&quot;no&quot;</code>, <code>&quot;n&quot;</code> or <code>&quot;0&quot;</code> is <code>false</code>.</li><li><code>&quot;true&quot;</code>, <code>&quot;yes&quot;</code>, <code>&quot;y&quot;</code> or <code>&quot;1&quot;</code> is <code>true</code>.</li><li>Any other string is an error.</li></ul><p>Note that environment variables are not supported for <a href="Arg/index.html#val-vflag"><code>Arg.vflag</code></a> and <a href="Arg/index.html#val-vflag_all"><code>Arg.vflag_all</code></a>.</p></header></section></section><section><header><h2 id="examples"><a href="#examples" class="anchor"></a>Examples</h2><p>These examples are in the <code>test</code> directory of the distribution.</p></header><section><header><h3 id="exrm"><a href="#exrm" class="anchor"></a>A <code>rm</code> command</h3><p>We define the command line interface of a <code>rm</code> command with the synopsis:</p><pre><code class="ml">rm [OPTION]... FILE...</code></pre><p>The <code>-f</code>, <code>-i</code> and <code>-I</code> flags define the prompt behaviour of <code>rm</code>, represented in our program by the <code>prompt</code> type. If more than one of these flags is present on the command line the last one takes precedence.</p><p>To implement this behaviour we map the presence of these flags to values of the <code>prompt</code> type by using <a href="Arg/index.html#val-vflag_all"><code>Arg.vflag_all</code></a>. This argument will contain all occurrences of the flag on the command line and we just take the <a href="Arg/index.html#val-last"><code>Arg.last</code></a> one to define our term value (if there's no occurrence the last value of the default list <code>[Always]</code> is taken, i.e. the default is <code>Always</code>).</p><pre><code class="ml">(* Implementation of the command, we just print the args. *)

type prompt = Always | Once | Never
let prompt_str = function
| Always -&gt; &quot;always&quot; | Once -&gt; &quot;once&quot; | Never -&gt; &quot;never&quot;

let rm prompt recurse files =
  Printf.printf &quot;prompt = %s\nrecurse = %B\nfiles = %s\n&quot;
    (prompt_str prompt) recurse (String.concat &quot;, &quot; files)

(* Command line interface *)

open Cmdliner

let files = Arg.(non_empty &amp; pos_all file [] &amp; info [] ~docv:&quot;FILE&quot;)
let prompt =
  let doc = &quot;Prompt before every removal.&quot; in
  let always = Always, Arg.info [&quot;i&quot;] ~doc in
  let doc = &quot;Ignore nonexistent files and never prompt.&quot; in
  let never = Never, Arg.info [&quot;f&quot;; &quot;force&quot;] ~doc in
  let doc = &quot;Prompt once before removing more than three files, or when
             removing recursively. Less intrusive than $(b,-i), while
             still giving protection against most mistakes.&quot;
  in
  let once = Once, Arg.info [&quot;I&quot;] ~doc in
  Arg.(last &amp; vflag_all [Always] [always; never; once])

let recursive =
  let doc = &quot;Remove directories and their contents recursively.&quot; in
  Arg.(value &amp; flag &amp; info [&quot;r&quot;; &quot;R&quot;; &quot;recursive&quot;] ~doc)

let cmd =
  let doc = &quot;remove files or directories&quot; in
  let man = [
    `S Manpage.s_description;
    `P &quot;$(tname) removes each specified $(i,FILE). By default it does not
        remove directories, to also remove them and their contents, use the
        option $(b,--recursive) ($(b,-r) or $(b,-R)).&quot;;
    `P &quot;To remove a file whose name starts with a `-', for example
        `-foo', use one of these commands:&quot;;
    `P &quot;rm -- -foo&quot;; `Noblank;
    `P &quot;rm ./-foo&quot;;
    `P &quot;$(tname) removes symbolic links, not the files referenced by the
        links.&quot;;
    `S Manpage.s_bugs; `P &quot;Report bugs to &lt;hehey at example.org&gt;.&quot;;
    `S Manpage.s_see_also; `P &quot;$(b,rmdir)(1), $(b,unlink)(2)&quot; ]
  in
  Term.(const rm $ prompt $ recursive $ files),
  Term.info &quot;rm&quot; ~version:&quot;v1.0.3&quot; ~doc ~exits:Term.default_exits ~man

let () = Term.(exit @@ eval cmd)</code></pre></header></section><section><header><h3 id="excp"><a href="#excp" class="anchor"></a>A <code>cp</code> command</h3><p>We define the command line interface of a <code>cp</code> command with the synopsis:</p><pre><code class="ml">cp [OPTION]... SOURCE... DEST</code></pre><p>The <code>DEST</code> argument must be a directory if there is more than one <code>SOURCE</code>. This constraint is too complex to be expressed by the combinators of <a href="Arg/index.html"><code>Arg</code></a>. Hence we just give it the <a href="Arg/index.html#val-string"><code>Arg.string</code></a> type and verify the constraint at the beginning of the <code>cp</code> implementation. If unsatisfied we return an <code>`Error</code> and by using <a href="Term/index.html#type-ret"><code>Term.ret</code></a> on the lifted result <code>cp_t</code> of <code>cp</code>, <code>Cmdliner</code> handles the error reporting.</p><pre><code class="ml">(* Implementation, we check the dest argument and print the args *)

let cp verbose recurse force srcs dest =
  if List.length srcs &gt; 1 &amp;&amp;
  (not (Sys.file_exists dest) || not (Sys.is_directory dest))
  then
    `Error (false, dest ^ &quot; is not a directory&quot;)
  else
    `Ok (Printf.printf
     &quot;verbose = %B\nrecurse = %B\nforce = %B\nsrcs = %s\ndest = %s\n&quot;
      verbose recurse force (String.concat &quot;, &quot; srcs) dest)

(* Command line interface *)

open Cmdliner

let verbose =
  let doc = &quot;Print file names as they are copied.&quot; in
  Arg.(value &amp; flag &amp; info [&quot;v&quot;; &quot;verbose&quot;] ~doc)

let recurse =
  let doc = &quot;Copy directories recursively.&quot; in
  Arg.(value &amp; flag &amp; info [&quot;r&quot;; &quot;R&quot;; &quot;recursive&quot;] ~doc)

let force =
  let doc = &quot;If a destination file cannot be opened, remove it and try again.&quot;in
  Arg.(value &amp; flag &amp; info [&quot;f&quot;; &quot;force&quot;] ~doc)

let srcs =
  let doc = &quot;Source file(s) to copy.&quot; in
  Arg.(non_empty &amp; pos_left ~rev:true 0 file [] &amp; info [] ~docv:&quot;SOURCE&quot; ~doc)

let dest =
  let doc = &quot;Destination of the copy. Must be a directory if there is more
             than one $(i,SOURCE).&quot; in
  Arg.(required &amp; pos ~rev:true 0 (some string) None &amp; info [] ~docv:&quot;DEST&quot;
         ~doc)

let cmd =
  let doc = &quot;copy files&quot; in
  let man_xrefs =
    [ `Tool &quot;mv&quot;; `Tool &quot;scp&quot;; `Page (2, &quot;umask&quot;); `Page (7, &quot;symlink&quot;) ]
  in
  let exits = Term.default_exits in
  let man =
    [ `S Manpage.s_bugs;
      `P &quot;Email them to &lt;hehey at example.org&gt;.&quot;; ]
  in
  Term.(ret (const cp $ verbose $ recurse $ force $ srcs $ dest)),
  Term.info &quot;cp&quot; ~version:&quot;v1.0.3&quot; ~doc ~exits ~man ~man_xrefs

let () = Term.(exit @@ eval cmd)</code></pre></header></section><section><header><h3 id="extail"><a href="#extail" class="anchor"></a>A <code>tail</code> command</h3><p>We define the command line interface of a <code>tail</code> command with the synopsis:</p><pre><code class="ml">tail [OPTION]... [FILE]...</code></pre><p>The <code>--lines</code> option whose value specifies the number of last lines to print has a special syntax where a <code>+</code> prefix indicates to start printing from that line number. In the program this is represented by the <code>loc</code> type. We define a custom <code>loc</code> <a href="Arg/index.html#argconv"><span>argument converter</span></a> for this option.</p><p>The <code>--follow</code> option has an optional enumerated value. The argument converter <code>follow</code>, created with <a href="Arg/index.html#val-enum"><code>Arg.enum</code></a> parses the option value into the enumeration. By using <a href="Arg/index.html#val-some"><code>Arg.some</code></a> and the <code>~vopt</code> argument of <a href="Arg/index.html#val-opt"><code>Arg.opt</code></a>, the term corresponding to the option <code>--follow</code> evaluates to <code>None</code> if <code>--follow</code> is absent from the command line, to <code>Some
Descriptor</code> if present but without a value and to <code>Some v</code> if present with a value <code>v</code> specified.</p><pre><code class="ml">(* Implementation of the command, we just print the args. *)

type loc = bool * int
type verb = Verbose | Quiet
type follow = Name | Descriptor

let str = Printf.sprintf
let opt_str sv = function None -&gt; &quot;None&quot; | Some v -&gt; str &quot;Some(%s)&quot; (sv v)
let loc_str (rev, k) = if rev then str &quot;%d&quot; k else str &quot;+%d&quot; k
let follow_str = function Name -&gt; &quot;name&quot; | Descriptor -&gt; &quot;descriptor&quot;
let verb_str = function Verbose -&gt; &quot;verbose&quot; | Quiet -&gt; &quot;quiet&quot;

let tail lines follow verb pid files =
  Printf.printf &quot;lines = %s\nfollow = %s\nverb = %s\npid = %s\nfiles = %s\n&quot;
    (loc_str lines) (opt_str follow_str follow) (verb_str verb)
    (opt_str string_of_int pid) (String.concat &quot;, &quot; files)

(* Command line interface *)

open Cmdliner

let lines =
  let loc =
    let parse s =
      try
        if s &lt;&gt; &quot;&quot; &amp;&amp; s.[0] &lt;&gt; '+' then Ok (true, int_of_string s) else
        Ok (false, int_of_string (String.sub s 1 (String.length s - 1)))
      with Failure _ -&gt; Error (`Msg &quot;unable to parse integer&quot;)
    in
    let print ppf p = Format.fprintf ppf &quot;%s&quot; (loc_str p) in
    Arg.conv ~docv:&quot;N&quot; (parse, print)
  in
  Arg.(value &amp; opt loc (true, 10) &amp; info [&quot;n&quot;; &quot;lines&quot;] ~docv:&quot;N&quot;
         ~doc:&quot;Output the last $(docv) lines or use $(i,+)$(docv) to start
               output after the $(i,N)-1th line.&quot;)

let follow =
  let doc = &quot;Output appended data as the file grows. $(docv) specifies how the
             file should be tracked, by its `name' or by its `descriptor'.&quot; in
  let follow = Arg.enum [&quot;name&quot;, Name; &quot;descriptor&quot;, Descriptor] in
  Arg.(value &amp; opt (some follow) ~vopt:(Some Descriptor) None &amp;
       info [&quot;f&quot;; &quot;follow&quot;] ~docv:&quot;ID&quot; ~doc)

let verb =
  let doc = &quot;Never output headers giving file names.&quot; in
  let quiet = Quiet, Arg.info [&quot;q&quot;; &quot;quiet&quot;; &quot;silent&quot;] ~doc in
  let doc = &quot;Always output headers giving file names.&quot; in
  let verbose = Verbose, Arg.info [&quot;v&quot;; &quot;verbose&quot;] ~doc in
  Arg.(last &amp; vflag_all [Quiet] [quiet; verbose])

let pid =
  let doc = &quot;With -f, terminate after process $(docv) dies.&quot; in
  Arg.(value &amp; opt (some int) None &amp; info [&quot;pid&quot;] ~docv:&quot;PID&quot; ~doc)

let files = Arg.(value &amp; (pos_all non_dir_file []) &amp; info [] ~docv:&quot;FILE&quot;)

let cmd =
  let doc = &quot;display the last part of a file&quot; in
  let man = [
    `S Manpage.s_description;
    `P &quot;$(tname) prints the last lines of each $(i,FILE) to standard output. If
        no file is specified reads standard input. The number of printed
        lines can be  specified with the $(b,-n) option.&quot;;
    `S Manpage.s_bugs;
    `P &quot;Report them to &lt;hehey at example.org&gt;.&quot;;
    `S Manpage.s_see_also;
    `P &quot;$(b,cat)(1), $(b,head)(1)&quot; ]
  in
  Term.(const tail $ lines $ follow $ verb $ pid $ files),
  Term.info &quot;tail&quot; ~version:&quot;%‌%VERSION%%&quot; ~doc ~exits:Term.default_exits ~man

let () = Term.(exit @@ eval cmd)</code></pre></header></section><section><header><h3 id="exdarcs"><a href="#exdarcs" class="anchor"></a>A <code>darcs</code> command</h3><p>We define the command line interface of a <code>darcs</code> command with the synopsis:</p><pre><code class="ml">darcs [COMMAND] ...</code></pre><p>The <code>--debug</code>, <code>-q</code>, <code>-v</code> and <code>--prehook</code> options are available in each command. To avoid having to pass them individually to each command we gather them in a record of type <code>copts</code>. By lifting the record constructor <code>copts</code> into the term <code>copts_t</code> we now have a term that we can pass to the commands to stand for an argument of type <code>copts</code>. These options are documented in a section called <code>COMMON
OPTIONS</code>, since we also want to put <code>--help</code> and <code>--version</code> in this section, the term information of commands makes a judicious use of the <code>sdocs</code> parameter of <a href="Term/index.html#type-info"><code>Term.info</code></a>.</p><p>The <code>help</code> command shows help about commands or other topics. The help shown for commands is generated by <code>Cmdliner</code> by making an appropriate use of <a href="Term/index.html#type-ret"><code>Term.ret</code></a> on the lifted <code>help</code> function.</p><p>If the program is invoked without a command we just want to show the help of the program as printed by <code>Cmdliner</code> with <code>--help</code>. This is done by the <code>default_cmd</code> term.</p><pre><code class="ml">(* Implementations, just print the args. *)

type verb = Normal | Quiet | Verbose
type copts = { debug : bool; verb : verb; prehook : string option }

let str = Printf.sprintf
let opt_str sv = function None -&gt; &quot;None&quot; | Some v -&gt; str &quot;Some(%s)&quot; (sv v)
let opt_str_str = opt_str (fun s -&gt; s)
let verb_str = function
  | Normal -&gt; &quot;normal&quot; | Quiet -&gt; &quot;quiet&quot; | Verbose -&gt; &quot;verbose&quot;

let pr_copts oc copts = Printf.fprintf oc
    &quot;debug = %B\nverbosity = %s\nprehook = %s\n&quot;
    copts.debug (verb_str copts.verb) (opt_str_str copts.prehook)

let initialize copts repodir = Printf.printf
    &quot;%arepodir = %s\n&quot; pr_copts copts repodir

let record copts name email all ask_deps files = Printf.printf
    &quot;%aname = %s\nemail = %s\nall = %B\nask-deps = %B\nfiles = %s\n&quot;
    pr_copts copts (opt_str_str name) (opt_str_str email) all ask_deps
    (String.concat &quot;, &quot; files)

let help copts man_format cmds topic = match topic with
| None -&gt; `Help (`Pager, None) (* help about the program. *)
| Some topic -&gt;
    let topics = &quot;topics&quot; :: &quot;patterns&quot; :: &quot;environment&quot; :: cmds in
    let conv, _ = Cmdliner.Arg.enum (List.rev_map (fun s -&gt; (s, s)) topics) in
    match conv topic with
    | `Error e -&gt; `Error (false, e)
    | `Ok t when t = &quot;topics&quot; -&gt; List.iter print_endline topics; `Ok ()
    | `Ok t when List.mem t cmds -&gt; `Help (man_format, Some t)
    | `Ok t -&gt;
        let page = (topic, 7, &quot;&quot;, &quot;&quot;, &quot;&quot;), [`S topic; `P &quot;Say something&quot;;] in
        `Ok (Cmdliner.Manpage.print man_format Format.std_formatter page)

open Cmdliner

(* Help sections common to all commands *)

let help_secs = [
 `S Manpage.s_common_options;
 `P &quot;These options are common to all commands.&quot;;
 `S &quot;MORE HELP&quot;;
 `P &quot;Use `$(mname) $(i,COMMAND) --help' for help on a single command.&quot;;`Noblank;
 `P &quot;Use `$(mname) help patterns' for help on patch matching.&quot;; `Noblank;
 `P &quot;Use `$(mname) help environment' for help on environment variables.&quot;;
 `S Manpage.s_bugs; `P &quot;Check bug reports at http://bugs.example.org.&quot;;]

(* Options common to all commands *)

let copts debug verb prehook = { debug; verb; prehook }
let copts_t =
  let docs = Manpage.s_common_options in
  let debug =
    let doc = &quot;Give only debug output.&quot; in
    Arg.(value &amp; flag &amp; info [&quot;debug&quot;] ~docs ~doc)
  in
  let verb =
    let doc = &quot;Suppress informational output.&quot; in
    let quiet = Quiet, Arg.info [&quot;q&quot;; &quot;quiet&quot;] ~docs ~doc in
    let doc = &quot;Give verbose output.&quot; in
    let verbose = Verbose, Arg.info [&quot;v&quot;; &quot;verbose&quot;] ~docs ~doc in
    Arg.(last &amp; vflag_all [Normal] [quiet; verbose])
  in
  let prehook =
    let doc = &quot;Specify command to run before this $(mname) command.&quot; in
    Arg.(value &amp; opt (some string) None &amp; info [&quot;prehook&quot;] ~docs ~doc)
  in
  Term.(const copts $ debug $ verb $ prehook)

(* Commands *)

let initialize_cmd =
  let repodir =
    let doc = &quot;Run the program in repository directory $(docv).&quot; in
    Arg.(value &amp; opt file Filename.current_dir_name &amp; info [&quot;repodir&quot;]
           ~docv:&quot;DIR&quot; ~doc)
  in
  let doc = &quot;make the current directory a repository&quot; in
  let exits = Term.default_exits in
  let man = [
    `S Manpage.s_description;
    `P &quot;Turns the current directory into a Darcs repository. Any
       existing files and subdirectories become ...&quot;;
    `Blocks help_secs; ]
  in
  Term.(const initialize $ copts_t $ repodir),
  Term.info &quot;initialize&quot; ~doc ~sdocs:Manpage.s_common_options ~exits ~man

let record_cmd =
  let pname =
    let doc = &quot;Name of the patch.&quot; in
    Arg.(value &amp; opt (some string) None &amp; info [&quot;m&quot;; &quot;patch-name&quot;] ~docv:&quot;NAME&quot;
           ~doc)
  in
  let author =
    let doc = &quot;Specifies the author's identity.&quot; in
    Arg.(value &amp; opt (some string) None &amp; info [&quot;A&quot;; &quot;author&quot;] ~docv:&quot;EMAIL&quot;
           ~doc)
  in
  let all =
    let doc = &quot;Answer yes to all patches.&quot; in
    Arg.(value &amp; flag &amp; info [&quot;a&quot;; &quot;all&quot;] ~doc)
  in
  let ask_deps =
    let doc = &quot;Ask for extra dependencies.&quot; in
    Arg.(value &amp; flag &amp; info [&quot;ask-deps&quot;] ~doc)
  in
  let files = Arg.(value &amp; (pos_all file) [] &amp; info [] ~docv:&quot;FILE or DIR&quot;) in
  let doc = &quot;create a patch from unrecorded changes&quot; in
  let exits = Term.default_exits in
  let man =
    [`S Manpage.s_description;
     `P &quot;Creates a patch from changes in the working tree. If you specify
         a set of files ...&quot;;
     `Blocks help_secs; ]
  in
  Term.(const record $ copts_t $ pname $ author $ all $ ask_deps $ files),
  Term.info &quot;record&quot; ~doc ~sdocs:Manpage.s_common_options ~exits ~man

let help_cmd =
  let topic =
    let doc = &quot;The topic to get help on. `topics' lists the topics.&quot; in
    Arg.(value &amp; pos 0 (some string) None &amp; info [] ~docv:&quot;TOPIC&quot; ~doc)
  in
  let doc = &quot;display help about darcs and darcs commands&quot; in
  let man =
    [`S Manpage.s_description;
     `P &quot;Prints help about darcs commands and other subjects...&quot;;
     `Blocks help_secs; ]
  in
  Term.(ret
          (const help $ copts_t $ Arg.man_format $ Term.choice_names $topic)),
  Term.info &quot;help&quot; ~doc ~exits:Term.default_exits ~man

let default_cmd =
  let doc = &quot;a revision control system&quot; in
  let sdocs = Manpage.s_common_options in
  let exits = Term.default_exits in
  let man = help_secs in
  Term.(ret (const (fun _ -&gt; `Help (`Pager, None)) $ copts_t)),
  Term.info &quot;darcs&quot; ~version:&quot;v1.0.3&quot; ~doc ~sdocs ~exits ~man

let cmds = [initialize_cmd; record_cmd; help_cmd]

let () = Term.(exit @@ eval_choice default_cmd cmds)</code></pre></header></section></section></div></body></html>